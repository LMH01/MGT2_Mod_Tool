plugins {
    id "java"
    id "application"
}

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencies {
    implementation 'org.slf4j:slf4j-simple:_'
    implementation 'org.slf4j:slf4j-api:_'
    implementation 'com.moandjiezana.toml:toml4j:_'
    implementation 'org.jetbrains:annotations:_'
    integrationTestCompile group: 'junit', name: 'junit', version: '4.13.2'
    integrationTestCompile 'org.slf4j:slf4j-api:_'
    integrationTestCompile 'org.slf4j:slf4j-simple:_'
    integrationTestCompile 'com.moandjiezana.toml:toml4j:_'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.13.2'
}

// comment in if log level should be temporarily set to debug
//run {
//    jvmArgs = ['-Dorg.slf4j.simpleLogger.defaultLogLevel=debug']
//}

sourceCompatibility = 1.21
targetCompatibility = 1.21
project.version = "5.1.0" //Version numbers that include "dev" are not checked for updates / tool will notify if update is available
archivesBaseName = "MGT2_Mod_Tool"

group = "com.github.lmh01"
application.mainClass = "com.github.lmh01.mgt2mt.MadGamesTycoon2ModTool"

// required so jar task includes dependencies
jar {
    manifest {
        attributes "Main-Class": application.mainClass
    }

    archiveVersion = ""

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task createZip(type: Zip, group: 'build') {
    dependsOn jar

    from tasks.jar.archivePath
    from ('assets')

    destinationDirectory = new File("build/releases")
    archiveFileName = "MGT2_Mod_Tool_${project.version}.zip"
}

task integrationTest(type: Test, group: 'verification') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

compileJava {options.encoding = "UTF-8"}

test {
    useJUnitPlatform()
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

// Generate version number during build time, used to be able to set the build date
// and git hash for dev versions
def generatedDir = "$buildDir/generated/sources/version"

task generateVersionFile {
    doLast {
        def timestamp = new Date().format("ddMMyyyy-HHmm")
        def version = "${project.version}"
        def gitHash = 'git rev-parse --short HEAD'.execute().text.trim()

        def packageDir = file("$generatedDir/com/github/lmh01/mgt2mt")
        packageDir.mkdirs()


        def file = new File(packageDir, "Version.java")
        file.text = """
            package com.github.lmh01.mgt2mt;

            public class Version {
                public static String getVersion() {
                    if ("${version}".contains("dev")) {
                        return "${version}-${gitHash}-${timestamp}";
                    } else {
                        return "${version}";
                    }
                }

                public static String getBuildGitHash() {
                    return "${gitHash}";
                }

                public static String getBuildDate() {
                    return "${timestamp}";
                }
            }
        """.stripIndent()
    }
}

compileJava.dependsOn generateVersionFile

sourceSets.main.java.srcDir generatedDir

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += "-Xlint:unchecked"
    source "$buildDir/generated/sources/version"
}